sample fn forward(ix : Int) -> Int {
  // choose a receiving neighbor
  // if there are n neighbors, sample from n-1.
  // if sample is < ix, return it
  // if sample is >= ix, return sample + 1
  s ~ discrete(1.0 / 3.0, 1.0 / 3.0, 1.0 / 3.0);
  if s < ix { s } else { s + 1 }
}

exact fn node(nid : Int) -> (Int, Int) {
  // an infected node will forward two more packets
  let p1 = sample(forward(nid)) in
  let p2 = sample(forward(nid)) in
  (p1, p2)
}
exact fn network_step(n0 : Bool, n1 : Bool, n2: Bool, n3 : Bool, next : Int) -> (Bool, Bool, Bool, Bool, Int, Int) {
  // at each step, an infected node sends new packets
  let fwd = node(next) in
  let _p1 = fwd[0] in
  let _p2 = fwd[1] in
  // we re-tabulate infections
  let _n0 = n0 || (_p1 == 0) || (_p2 == 0) in
  let _n1 = n1 || (_p1 == 1) || (_p2 == 1) in
  let _n2 = n2 || (_p1 == 2) || (_p2 == 2) in
  let _n3 = n3 || (_p1 == 3) || (_p2 == 3) in
  // and return the new state
  (_n0, _n1, _n2, _n3, _p1, _p2)
}

sample fn as_num(b : Bool) -> Float {
  if (b) { 1.0 } else { 0.0 }
}

sample {
  // first we sample two packets from the starting node 0
  p <- exact(node(0));
  p1  <- p[0];
  p2  <- p[1];
  i0 <- true;
  i1 <- false;
  i2 <- false;
  i3 <- false;

  q  <- [];
  q  <- push(q, p1);
  q  <- push(q, p2);
  num_steps <- 3;
  while (num_steps > 0) {
    nxt <- head(q);
    q   <- tail(q);
    state <- exact(network_step(i0, i1, i2, i3, nxt));
    i0 <- state[0];
    i1 <- state[1];
    i2 <- state[2];
    i3 <- state[3];
    q   <- push(q, state[4]);
    q   <- push(q, state[5]);
    num_steps <- num_steps - 1;
    true
  };
  n0 <- as_num(i0);
  n1 <- as_num(i1);
  n2 <- as_num(i2);
  n3 <- as_num(i3);
  (n0 + n1 + n2 + n3)
}
